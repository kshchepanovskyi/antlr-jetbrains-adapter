buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://oss.sonatype.org/content/groups/public' }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.ksprojects:java-code-style:1.1'
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.7"
    id "com.jfrog.bintray" version "1.7.3"
}

group = 'org.antlr'
description = 'Support for using ANTLR-generated parsers/lexers in jetbrains IDE plug-ins.'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

apply plugin: 'antlr'

dependencies {
    antlr 'org.antlr:antlr4:4.7'
    compile 'org.antlr:antlr4-runtime:4.7'
    compile 'com.google.guava:guava:21.0'
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    version ideaVersion
}

apply plugin: 'java'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        //noinspection SpellCheckingInspection
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from sourceSets.main.allJava
    classifier = 'javadoc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

// ~/gradle.properties should contain repo credentials:
// bintrayUser = 'user'
// bintrayApiKey = 'your-api-key'
// }
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['Main']
    pkg {
        repo = 'maven'
        name = rootProject.name
        userOrg = 'antlr'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kshchepanovskyi/antlr4-jetbrains-adapter'
        publicDownloadNumbers = true
        version {
            name = rootProject.version
            desc = 'Jetbrains-ANTLR support library'
            released  = new Date()
            vcsTag = rootProject.version
        }
    }
}

apply plugin: 'checkstyle'
checkstyle {
    config = resources.text.fromString(getClass().getResource("checkstyle.xml").text)
    toolVersion = '7.6.1'

    // Exclude generated code from Checkstyle checks
    checkstyleMain.source = "src/main/java"
    checkstyleTest.source = "src/test/java"
}
